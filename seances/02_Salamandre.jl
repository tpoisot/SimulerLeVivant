# ---
# title: S√©ance 2
# topic: Les t√¢ches et les rayures
# author: Timoth√©e Poisot
# ---

# # Concepts principaux

# ## Nombres (pseudo)-al√©atoires 

# Quand on g√©n√®re des nombres al√©atoires, avec les fonctions comme `rand`, les
# r√©sultats sont diff√©rents √† chaque fois. Pour faciliter la comparaison des
# r√©sultats, il est utile d'utiliser un `seed` , qui repr√©sente le point de
# d√©part de l'algoritme des nombres (pseudo)-al√©atoires.

# Cette initialisation se fait de la fa√ßon suivante:

import Random
Random.seed!(2045)

# Les nombres qui sont g√©n√©r√©s apr√®s cette commande seront toujours les m√™mes,
# mais vont respecter les propri√©t√©s des nombres al√©atoires. La bonne pratique
# est d'utiliser cette commande apr√®s avoir charg√© les packages, et avant le
# code.

# ## Indexation basique dans les matrices

# Dans la s√©ance pr√©c√©dente, nous avons vu comment cr√©er des matrices, et
# comment lire le contenu √† une ligne et colonne particuli√®re. On peut, en
# pratique, faire beaucoup plus avec des matrices.

# Prenons l'exemple de la matrice suivante, avec trois lignes et cinq colonnes,
# qui contient des nombres al√©atoires entiers entre 1 et 5:

V = rand(1:5, 3, 5)

# On peut acc√©der √† la premi√®re ligne de cette matrice avec

V[1, :]

# et √† sa deuxi√®me colonne avec

V[:, 2]

# On peut aussi prendre les deux premi√®res lignes, et les trois derni√®res
# colonnes, avec

V[begin:(begin+1), (end-2):end]

# Ce qui est la m√™me chose que 

V[1:2, 3:5]

# mais sans avoir besoin d'avoir les coordon√©es exactes de la derni√®re colonne.

# ## Indexation avanc√©e dans les matrices

# Les matrices ont toutes un syst√®me de coordon√©es, qui sont soit les coordon√©es
# Cart√©siennes:

collect(CartesianIndices(V))

# soit les coordon√©es lin√©aires:

collect(LinearIndices(V))

# Notez que les coordon√©es lin√©aires suivent les colonnes: Julia est un langage
# _column-major_, qui va stocker les colonnes ensemble dans la m√©moire. Si on
# veut am√©liorer la performance de nos simulations, op√©rer sur les colonnes sera
# souvent beaucoup plus rapide que d'op√©rer sur les lignes.

# Une caract√©ristique importante des indices est qu'ils sont _relatifs_. Par
# exemple, si on veut exprimer la position qui est "la cellule √† gauche de la
# position 3, 4", on peut l'√©crire

CartesianIndex(3, 4) + CartesianIndex(-1, 0)

# La position `CartesianIndex(-1, 0)` signifie: une colonne avant, sur la m√™me
# ligne. Nous allons _beaucoup_ utiliser cette propri√©t√© pour nous d√©placer
# rapidement dans des matrices.

# ## Iteration

# Dans la s√©ance pr√©c√©dente, nous avions utilis√© une boucle `for`, qui
# permettait de r√©p√©ter un processus plusieurs fois. Dans cette s√©ance, nous
# allons formaliser ce concept, qui est fondamental pour le reste du cours.

# Une boucle `for` est une structure qui s'√©crit en g√©n√©ral de la mani√®re
# suivante:

# ~~~ raw
# for ELEMENT in COLLECTION
#   instructions
# end
# ~~~

# La variable `ELEMENT` n'existe pas en dehors de la boucle. C'est une nuance
# importante: elle est cr√©√©e par la boucle, et d√©truite quand la boucle est
# termin√©e. Une boucle `for` va simplement prendre chaque valeur de
# `COLLECTION`, les stocker dans `ELEMENT`, et on pourra donc appliquer des
# op√©rations de mani√®re it√©rative.

# Par exemple, si on veut multiplier par deux tous les √©l√©ments du vecteur `[1,
# 2, 3, 4]`, et afficher le r√©sultat sur une nouvelle ligne avec `println`, on
# peut utiliser une boucle `for`:

for x in [1, 2, 3, 4]
    println(2x)
end

# ## Iteration avanc√©e dans les matrices

# On peut traverser des matrices de fa√ßon beaucoup plus efficace en combinant
# les boucles `for` et les techniques d'indexation. Pour rappel, dans cette
# section, nous utilisons la matrice suivante:

V = rand(1:9, 3, 4)

# Par exemple, on peut prendre chaque √©l√©ment d'une matrice sans devoir
# sp√©cifier les lignes et les colonnes:

for v in V
    println(v)
end

# Remarquez que l'ordre des √©l√©ments suit ici le `LinearIndex`. On peut aussi
# aller chercher directement les indices des matrices:

for i in eachindex(V)
    println(i)
end

# Mais les indices sont eux-m√™me retourn√©s sous forme de matrice. On peut donc
# it√©rer sur les indices Cart√©siens:

for ci in CartesianIndices(V)
    println(ci)
end

# Cette structure est particuli√®rement utile, parce que nous aurons souvent
# besoin de faire des t√¢ches comme: pour chaque cellule, prendre la cellue du
# dessus, et si cette cellule est dans la matrice, effectuer une op√©ration sur
# sa valeur.

for position in CartesianIndices(V)
    dessous = position + CartesianIndex(0, -1)
    if dessous in CartesianIndices(V)
        println(dessous)
    end
end

# On utilise ici la structure `if un truc in plusieurs trucs`, qui renvoie
# `true` si l'√©l√©ment `un truc` fait partie de la collection `plusieurs trucs`.

# On peut enfin it√©rer d'une mani√®re qui nous renvoie √† la fois la position et
# la valeur:

for (position, valeur) in enumerate(V)
    println("La position $position contient la valeur $valeur")
end

# ## Fonctions (moins que le minimum n√©cessaire!)

# Lors de la derni√®re s√©ance, nous avions mis des instructions dans une boucle
# `for`. Pour rendre le code plus lisible, il est souvent pertinent de regrouper
# des op√©rations similaires dans des fonctions. Dans les s√©ances qui viennent,
# nous allons introduire des fa√ßons plus complexes (et flexibles!) de d√©finir
# des fonctions. Pour le moment nous allons utiliser la syntaxe suivante:

# ~~~raw
# fonction nom(arg1, arg2, arg3)
#   [operations sur arg1, arg2, arg3]
#   return resultat
# end
# ~~~

# Par example, on peut d√©finir une fonction qui additione ses deux arguments
# avec:

function addition(entree1, entree2)
    resultat = entree1 + entree2
    return resultat
end

#-

addition(1, 2)

#-

addition(3.0, 2.5)

# Comme avec les boucles, les variables qui sont d√©clar√©es dans la fonction
# n'existent _que_ dans la fonction.

# Au cours de la session, nous allons _consid√©rablement_ complexifier les t√¢ches
# que l'on peut faire en d√©clarant des fonctions, en introduisant notamment des
# valeurs par d√©faut, des mot-cl√©s, puis enfin des restrictions sur le type des
# entr√©es. Pour cette s√©ance, cette compr√©hension de base est suffisante.

# # Un automate cellulaire pour la pigmentation

# Avec cette simulation, nous voulons observer la pigmentation d'un tissu en
# utilisant une s√©rie de r√®gles simples qui vont approximer un mod√®le dit de
# r√©action/diffusion. Ces mod√®les ont √©t√© introduits par Alan Turing dans les
# ann√©es 1950 @turing1952chemical.

# Le mod√®le d'origine utilise des √©quations diff√©rentielles partielles pour
# mod√©liser la diffusion, mais on peut approximer les m√™me m√©canismes avec un
# automate cellulaire. Nous allons d'abord d√©finir le probl√®me et son √©tat
# initial, puis introduire les diff√©rents r√®gles.

# Ce mod√®le repr√©sente un tissu (la peau ou le pelage d'un animal) comme un
# espace en deux dimensions, dans lequel chaque position (cellule dans une
# lattice) est soit pigment√©e (`true`), soit non-pigment√©e (`false`).

# ## √âtat initial

# Le tissu a une dimension qui reste fixe pendant toute la simulation. Notez
# qu'ici on d√©finit deux variables sur la m√™me ligne. C'est un raccourci
# d'√©criture qui n'est pas n√©cessaire.

lignes, colonnes = 205, 155

# On d√©finit ensuite une probabilit√© que les cellules soient initialement
# pigment√©es. Puisque c'est une probabilit√©, ce nombre devrait √™tre entre 0 et
# 1.

p_activation = 0.01

# Pour l'√©tat initial, on va devoir parcourir une grille de taille `lignes`,
# `colonnes`, et pour chaque cellule, lui donner la valeur qui correspond √† la
# pigmentation (`true`) avec une probabilit√© `p_activation`.

function etat_initial(rows, cols, p_activation)
    lattice = zeros(Bool, rows, cols)
    for row in 1:rows
        for col in 1:cols
            lattice[row, col] = rand() < p_activation
        end
    end
    return lattice
end

# Cette fonction utilise `rand()`, qui par d√©faut g√©n√®re un nombre al√©atoire
# entre 0 et 1, avec une distribution uniforme.

# Avec ces informations, on peut maintenant cr√©er notre lattice:

lattice = etat_initial(lignes, colonnes, p_activation);

# On n'affiche pas cette lattice, qui peut √™tre tr√®s grande.

# Ici, on choisit d'appeller cet object `lattice`, puisque c'est ce qu'il
# repr√©sente. Mais on peut donner un nom plus explicite √† cet object, comme par
# exemple `pelage`, ou encore `ü¶ì`. Julia accepte la majorit√© des [symboles
# unicode](https://docs.julialang.org/en/v1/manual/unicode-input/). Il se peut
# que votre police de caract√®re ne les affiche pas tous --- celles qui ont le
# plus de support sont [Iosevka](https://typeof.net/Iosevka/) (ma pr√©f√©r√©e),
# [JuliaMono](https://juliamono.netlify.app/#) (utilis√©e dans ces notes de
# cours), et dans une moindre mesure, [Noto Sans
# Mono](https://fonts.google.com/noto/specimen/Noto+Sans+Mono) et [JetBrains
# Mono](https://www.jetbrains.com/lp/mono/). Elles sont toutes gratuites.

# ## R√®gles biologiques

# Dans notre simulation du mod√®le de r√©action/diffusion, une cellule va
# s'activer si le signal qui encourage son activation est plus grand que le
# signal qui encourage sa d√©sactivation. Ces deux signaux se calculent de la
# m√™me fa√ßon: le nombre de voisins actifs, multipli√© par le poids du signal
# d'activation.

# Autrement dit, une cellule se pigmente _si_ $w_a N_a > w_i N_i$, avec $w_a$ et
# $w_i$ les poids de l'activation et de l'inhibition, et $N_a$ et $N_i$ le
# nombre de cellules voisines qui sont activ√©es et inhib√©es.

# Ce mod√®le repr√©sente une situation dans laquelle une cellule est activ√©e en
# r√©ponse √† la diffusion de deux substances: les cellules activ√©es diffusent √†
# la fois une substance activatrice et une substance inhibitrice. Les poids
# $w_a$ et $w_i$ mesurent l'affinit√© des cellules pour ces substances, et on
# peut modifier le rayon de diffusion des substances en calculant le nombre de
# voisins dans un voisinage toujours plus grand.

# ## Identification des voisins

# Nous allons devoir calculer les voisins qui sont actifs tr√®s souvents, et donc
# c'est une bonne occasion de cr√©er une fonction pour localiser ces voisins.
# Nous allons supposer que nous cherchons des voisins qui sont dans un cercle
# d'un diam√®tre donn√©.

# Un point est dans un cercle si la distance Euclidienne entre ce point et le
# centre du cercle est inf√©rieure ou √©gale au rayon du cercle. Nous allons
# pouvoir identifier les points via leurs `CartesianIndex`. Par exemple, le
# point $(1, 1)$ est √† une distance $\approx 1.41$ du centre du plan:

sqrt(sum(Tuple(CartesianIndex(0, 0) - CartesianIndex(1, 1)) .^ 2))

# **NB**: l'appel √† `Tuple` est n√©cessaire ici. C'est comme √ßa. Tout n'a pas
# toujours une explication satisfaisante.

function point_dans_cercle(centre, point, rayon)
    d·µ¢‚±º = sqrt(sum(Tuple(centre - point) .^ 2))
    return d·µ¢‚±º <= rayon
end

# Cette fonction va donc nous permettre, pour chaque point de la matrice, de
# trouver ses voisins. Par exemple, si on prend la position `[10, 15]` et qu'on
# cherche les points dans un rayon de 2 cellules, on obtient:

for cellule in CartesianIndices(lattice)
    if point_dans_cercle(CartesianIndex(3, 5), cellule, 2)
        println(cellule)
    end
end

# Mais en pratique, on veut faire une t√¢che un peu plus compliqu√©e: pour chaque
# cellule, on souhaite compter ses voisins actifs, dans deux rayons possiblement
# diff√©rents. On va donc commencer par faire une boucle sur chaque cellule:

# ~~~ raw
# for cellule in eachindex(lattice)
#   1) identifier tous les voisins
#   2) identifier ceux qui sont actifs
# end
# ~~~

# Il existe une fonction, `findall`, qui permet d'identifier toutes les
# positions d'une collection qui correspondent √† un crit√®re donn√©. Si on donne
# uniquement une collection comme argument, `findall` renvoie les positions pour
# lesquelle cette collection a la valeur `true`. On peut donc √©conomiser du
# temps, en limitant notre recherche aux cellules actives:

centre = CartesianIndex(10, 12)
for cellule_active in findall(lattice)
    if point_dans_cercle(centre, cellule_active, 3)
        println(cellule_active)
    end
end

# On peut donc maintenant calculer le nombre de voisins:

function nombre_de_voisins(cellule, cellules_actives, rayon_activation, rayon_inhibition)
    na = 0
    ni = 0
    for voisin in cellules_actives
        if point_dans_cercle(cellule, voisin, rayon_activation)
            na += 1
        end
        if point_dans_cercle(cellule, voisin, rayon_inhibition)
            ni += 1
        end
    end
    return (na, ni)
end

# Cette approche du probl√®me est assez lente, mais notre objectif pour cette
# s√©ance est de d√©composer le probl√®me au maximum. Dans la pratique, on peut
# employer plusieurs m√©thodes pour (i) identifier une r√©gion carr√©e de la
# matrice dans laquelle les cellules pertinentes sont contenues, et (ii)
# identifier lequel des deux rayons est le plus grand pour ne v√©rifier le second
# rayon que si c'est pertinent. Ces a√©m√©liorations rendraient la fonction
# beaucoup plus rapide, mais aussi plus longue.

nombre_de_voisins(CartesianIndex(10, 12), findall(lattice), 12, 15)

# ## Mise √† jour de l'activation des cellules

# Pour changer l'activation des cellules, il faut calculer le nouvel √©tat de
# chaque cellule, sans interf√©rer avec l'√©tat actuel. Nous allons donc devoir
# cr√©er une nouvelle lattice pour la g√©n√©ration suivante:

prochaine_lattice = zeros(Bool, size(lattice));

# Remarquez qu'on utilise `size(lattice)` pour que la lattice aie la m√™me
# taille.

# On d√©finit maintenant les param√®tres de la simulation:

wa = 0.25   # Poids de l'activation
wi = 1.0  # Poids de l'inhibition
Ra = 3.5  # Rayon d'activation
Ri = 6  # Rayon d'inhibition

# Puis on simule la premi√®re g√©n√©ration:

cellules_actives = findall(lattice)

for cellule in CartesianIndices(lattice)
    na, ni = nombre_de_voisins(cellule, cellules_actives, Ra, Ri)
    prochaine_lattice[cellule] = wa * na > wi * ni
end

# ## Simulation

# Il faut maintenant r√©p√©ter cette op√©ration plusieurs fois:

temps = 5  # Nombre de g√©n√©rations √† simuler

#-

lattice = etat_initial(lignes, colonnes, p_activation);
#-

for generation in 1:temps
    cellules_actives = findall(lattice)
    prochaine_lattice = zeros(Bool, size(lattice))
    for cellule in CartesianIndices(lattice)
        ni, na = nombre_de_voisins(cellule, cellules_actives, Ra, Ri)
        prochaine_lattice[cellule] = wa * na > wi * ni
    end
    for cellule in CartesianIndices(lattice)
        lattice[cellule] = prochaine_lattice[cellule]
    end
end

# On peut enfin visualiser le r√©sultat:

using CairoMakie

#-

heatmap(
    ## On passe d'abord l'objet a visualiser
    lattice,
    ## Puis on fixe les deux couleurs
    ## pour resp. `false` et `true`
    colormap=[:midnightblue, :gold],
    colorrange = (0, 1),
    ## On sp√©cifie que les cellules du heatmap
    ## sont des carr√©s
    axis=(; aspect=DataAspect()),
    ## Et on fixe enfin un plus grand nombre de pixles pour avoir
    ## une meilleure r√©solution
    figure=(; figure_padding=0)
)

## On termine enfin cette figure en retirant les axes et les graduations,
## puis en affichant la figure finale
hidespines!(current_axis())
hidedecorations!(current_axis())
current_figure()

# Est-ce que ce r√©sultat ressemble √† des motifs qu'on peut observer dans la
# nature? Que pensez-vous que changer le rayon, ou la force de l'activation
# ferait sur la coloration de cette surface? Est-ce que √ßa pourrait avoir un
# effet sur la forme des motifs?